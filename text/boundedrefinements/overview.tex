\section{Overview}\label{sec:boundedrefinements:overview}

We start with a high level overview of bounded refinement types.
%
We first present a short introduction to refinement type specifications, 
to make this chapter self contained.
%
Then, we introduce bounded refinements,
and show how they permit \emph{modular} higher-order specifications.
%
Finally, we describe how they are implemented via an elaboration
process that permits \emph{automatic} first-order verification.

\subsection{Preliminaries}

\mypara{Refinement Types} let us precisely specify subsets of values,
by conjoining base types with logical predicates that constrain the values.
We get decidability of type checking, by limiting these predicates to
decidable, quantifier-free, first-order logics, including the theory
of linear arithmetic, uninterpreted functions, arrays, bit-vectors
and so on. 
%
For example, the refinement types
%
\begin{code}
  type Pos     = {v:Int | 0 < v}
  type IntGE x = {v:Int | x <= v}
\end{code}
%
specify subsets of @Int@ corresponding to values
that are positive or larger than some other value @x@
respectively. 
%
We can use refinement types to specify contracts
like pre- and post-conditions by suitably refining the input
and output types of functions.

\mypara{Preconditions} are specified by refining input types.
We specify that the function @assert@ must
\emph{only} be called with @True@,
%
where the type @TRUE@ contains only the singleton @True@:
%
\begin{code}
  type TRUE = {v:Bool | v <=> True}

  assert         :: TRUE -> a -> a
  assert True x  = x
  assert False _ = error "Provably Dead Code"
\end{code}

\mypara{We can specify post-conditions} by refining output types.
For example, a primitive @Int@ comparison operator @leq@ can be
assigned a type that says that the output is @True@ iff the
first input is actually less than or equal to the second:
%
\begin{mcode}
  leq :: x:Int -> y:Int -> {v:Bool | v  <=> x <= y}
\end{mcode}

\mypara{Refinement Type Checking} proceeds by checking that at each
application, the types of the actual arguments are \emph{subtypes}
of those of the function inputs, in the environment (or context) in
which the call occurs.
%
Consider the function:
%
\begin{code}
  checkGE     :: a:Int -> b:IntGE a -> Int
  checkGE a b = assert cmp b
    where cmp = a `leq` b
\end{code}
%
To verify the call to @assert@ we check that
the actual parameter @cmp@ is a subtype of @TRUE@,
under the assumptions given by the input types for
@a@ and @b@.
%
Via subtyping~\cite{LiquidICFP14} the check reduces to establishing
the validity of the \emph{verification condition}~(VC)
%
\begin{code}
  a <= b => (cmp <=> a <= b) => v = cmp => (v<=>true)
\end{code}
%
The first antecedent comes from the input type of @b@, the
second from the type of @cmp@ obtained from the output of @leq@,
the third from the \emph{actual} input passed to @assert@,
and the goal comes from the input type \emph{required} by @assert@.
%
An SMT solver \cite{NelsonOppen} readily establishes the validity
of the above VC, thereby verifying @checkGE@.

\begin{comment}

% \subsection{Abstract Refinements}

\mypara{First order refinements prevent modular specifications.}
Consider the function that returns the largest element of a list:
%
\begin{code}
  maximum         :: List Int -> Int
  maximum [x]     = x
  maximum (x:xs)  = max x (maximum xs)
    where max a b = if a < b then b else a
\end{code}
%
How can one write a first-order refinement type specification for
@maximum@ that will let us verify the below code?
%
\begin{code}
  posMax :: List Pos -> Pos
  posMax = maximum
\end{code}
%
%   posMax :: List Neg -> Neg
%   posMax = maximum
%
Any suitable specification would have to enumerate the
situations under which @maximum@ may be invoked
breaking modularity.

\mypara{Abstract Refinements} overcome the above modularity
problems \cite{vazou13}.
%
The main idea is that we can type @maximum@ by observing
that it returns \emph{one of} the elements in its input list.
Thus, if every element of the list enjoys some refinement @p@
then the output value is also guaranteed to satisfy @p@.
%
Concretely, we can type the function as:
%
\begin{code}
maximum :: forall<p::Int->Bool>. List Int<p> -> Int<p>
\end{code}
%
where informally, @Int<p>@ stands for @{v:Int | p v}@,
and @p@ is an \emph{uninterpreted function} in the refinement
logic~\cite{NelsonOppen}.
%
The signature states that for any refinement @p@ on @Int@,
the input is a list of elements satisfying @p@
and returns as output an integer satisfying @p@.
%
In the sequel, we will drop the explicit quantification
of abstract refinements; all free abstract refinements
will be \emph{implicitly} quantified at the top-level
(as with classical type parameters.)

\paragraph{Abstract Refinements Preserve Decidability.}
Abstract refinements do not require the use of higher-order
logics. Instead, abstractly refined signatures (like @maximum@)
can be verified by viewing the abstract refinements @p@ as
uninterpreted functions that only satisfy the axioms of
congruence, namely:
%
\begin{code}
  forall x y. x = y => p x <=> p y
\end{code}
%
As the quantifier free theory of uninterpreted functions
is decidable \cite{NelsonOppen}, abstract refinement type
checking remains decidable \cite{vazou13}.

\paragraph{Abstract Refinements are Automatically Instantiated} at call-sites,
via the abstract interpretation framework of Liquid Typing~\cite{vazou13}.
Each instantiation yields fresh refinement variables on
which subtyping constraints are generated; these constraints
are solved via abstract interpretation yielding the instantiations.
%
Hence, we verify @posMax@ % and @negMax@
by instantiating:
%
\begin{code}
  p |-> \ v -> 0 < v   -- at posMax
\end{code}
  % p |-> \ v -> v < 0   -- at negMax

\end{comment}

\subsection{Bounded Refinements}

Refinement types hit various expressiveness walls, 
as for decidability, refinements are constraint to 
first order, decidable logics.
%
Consider the following example
from~\cite{TerauchiPOPL13}.
%
@find@ takes as input a predicate @q@, a continuation
@k@ and a starting number @i@; it proceeds to compute
the smallest @Int@ (larger than @i@) that satisfies
@q@, and calls @k@ with that value.
%
@ex1@ passes @find@ a continuation that checks that the
``found'' value equals or exceeds @n@.
%
\begin{code}
  ex1 :: (Int -> Bool) -> Int -> ()
  ex1 q n = find q (checkGE n) n

  find q k i
    | q i       = k i
    | otherwise = find q k (i + 1)
\end{code}

\mypara{Verification fails} as there is no way to specify that
@k@ is only called with arguments greater than @n@.
%
First, the variable @n@ is not in scope at the function
definition so we cannot refer to it.
%
Second, we could try to say that @k@ is invoked with values
greater than or equal to @i@, which gets substituted with @n@
at the call-site. Alas, due to the currying order, @i@ too is
not in scope at the point where @k@'s type is defined so
the type for @k@ cannot depend upon @i@.

\mypara{Can Abstract Refinements Help?} Lets try to
use Abstract Refinements, from chapter~\ref{chapter:abstractrefinements},
to abstract over the refinement that @i@ enjoys, and
assign @find@ the type:
%
\begin{code}
  find :: (Int -> Bool) -> (Int<p> -> a) -> Int<p> -> a
\end{code}
%
which states that for any refinement @p@, the function takes
an input @i@ which satisfies @p@ and hence that the continuation
is also only invoked on a value which trivially enjoys @p@, namely @i@.
%
At the call-site in @ex1@ we can instantiate
\begin{equation}
\cc{p} \mapsto \lambda \cc{v} \rightarrow \cc{n} \leq \cc{v} \label{eq:inst:find}
\end{equation}
%
This instantiated refinement is satisfied by the parameter @n@ and is
sufficient to verify, via function subtyping, that @checkGE n@ will
only be called with values satisfying @p@, and hence larger than @n@.

\mypara{The function find is ill-typed} as the signature requires that
at the recursive call site, the value @i+1@ \emph{also}
satisfies the abstract refinement @p@.
%
While this holds for the example we have in mind~(\ref{eq:inst:find}),
it does not hold \emph{for all} @p@, as required by the type of @find@!
%
Concretely, @{v:Int | v = i + 1}@ is in general \emph{not} a subtype of
@Int<p>@, as the associated VC
% Concretely, the recursive call generates the VC
%
\begin{equation}
    ... \Rightarrow \cc{p i} \Rightarrow \cc{p (i+1)} \label{eq:vc:find}
\end{equation}
%
%\begin{equation}
%p\ i \Rightarrow p\ (i + 1) \label{eq:vc:find}
%\end{equation}
%
is \emph{invalid} -- the type checker thus (soundly!) rejects @find@.

\mypara{We must Bound the Quantification} of @p@ to limit
it to refinements satisfying some constraint, in this case
that @p@ is \emph{upward closed}. In the dependent setting,
where refinements may refer to program values, bounds
are naturally expressed as constraints between refinements.
% Horn clauses over refinements.
%
We define a bound, @UpClosed@
%
which states that @p@ is a refinement that is \emph{upward closed},
\ie satisfies @forall x. p x =>  p (x+1)@,
and use it to type @find@ as:
%
\begin{code}
  bound UpClosed (p :: Int -> Bool)
    = \x -> p x => p (x+1)

  find :: (UpClosed p) => (Int -> Bool)
                       -> (Int<p> -> a)
                       ->  Int<p> -> a
\end{code}
%
This time, the checker is able to use the bound to
verify the VC~(\ref{eq:vc:find}).
%
We do so in a way that refinements (and thus VCs) remain quantifier
free and hence, SMT decidable~(\S~\ref{sec:overview:implementation}).

\mypara{At the call} to @find@ in the body of @ex1@, we perform
the instantiation~(\ref{eq:inst:find}) which generates the
\emph{additional} VC
%
\hbox{@n <=  x => n <=  x+1@}
%
by plugging in the concrete refinements to the bound constraint.
%
The SMT checks the validity of the VC
and hence this instantiation, thereby statically
verifying @ex1@, \ie that the assertion inside
@checkGE@ cannot fail.
%

\subsection{Bounds for Higher-Order Functions}

Next, we show how bounds expand the scope of refinement typing by
letting us write precise modular specifications for various canonical
higher-order functions.

\subsubsection{Function Composition}\label{sec:compose}

First, consider @compose@. What is a modular specification
for @compose@ that would let us verify that @ex2@ enjoys
the given specification?
%
\begin{code}
  compose f g x = f (g x)

  type Plus x y = {v:Int | v = x + y}
  
  ex2    :: n:Int -> Plus n 2
  ex2    = incr `compose` incr

  incr   :: n:Int -> Plus n 1
  incr n = n + 1
\end{code}

\mypara{The challenge is to chain the dependencies} between the
input and output of @g@ and the input and output of @f@ to
obtain a relationship between the input and output of the
composition. We can capture the notion of chaining in a bound:
%
%% f -> p
%% g -> q
\begin{code}
  bound Chain p q r = \x y z ->
        q x y => p y z => r x z
\end{code}
%
which states that for any @x@, @y@ and @z@, if
%
(1) @x@ and @y@ are related by @q@, and
(2) @y@ and @z@ are related by @p@, then
(3) @x@ and @z@ are related by @r@.

We use @Chain@ to type @compose@ using three abstract
refinements @p@, @q@ and @r@, relating the arguments
and return values of @f@ and @g@ to their composed value.
%
(Here, @c<r x>@ abbreviates @{v:c | r x v}@).

\begin{code}
  compose :: (Chain p q r) => (y:b -> c<p y>)
                           -> (x:a -> b<q x>)
                           -> (w:a -> c<r w>)
\end{code}

\mypara{To verify} @ex2@ we instantiate, at the call to @compose@,
%
\begin{code}
  p, q |-> \x v -> v = x + 1
     r |-> \x v -> v = x + 2
\end{code}
%
The above instantiation satisfies the bound, as shown by the validity
of the VC derived from instantiating @p@, @q@, and @r@ in @Chain@:
%
\begin{code}
  y = x + 1 => z = y + 1 => z == x + 2
\end{code}
%
and hence, we can check that @ex2@ implements its specified type.


\subsubsection{List Filtering}

Next, consider the list @filter@ function.
%
What type signature for @filter@ would let us check @positives@?
\begin{code}
  filter q (x:xs)
    | q x         = x : filter q xs
    | otherwise   = filter q xs
  filter _ []     = []

  positives       :: [Int] -> [Pos]
  positives       = filter isPos
    where isPos x = 0 < x
\end{code}
%
Such a signature would have to relate the @Bool@ returned by
@f@ with the property of the @x@ that it checks for.
%
Typed Racket's latent predicates~\cite{typedracket}
account for this idiom, but are a special construct
limited to @Bool@-valued ``type'' tests, and not
arbitrary invariants.
%
Another approach is to avoid the so-called
``Boolean Blindness'' that accompanies
@filter@ by instead using option types
and @mapMaybe@.

\mypara{We overcome blindness using a witness} bound:
%
\begin{code}
  bound Witness p w = \x b -> b => w x b => p x
\end{code}
%
which says that @w@ \emph{witnesses} the
refinement @p@. That is, for any boolean @b@ such
that @w x b@ holds, if @b@ is @True@ then @p x@ also holds.

\mypara{We can give} @filter@ a type that says that the output values
enjoy a refinement @p@ as long as the test predicate @q@ returns
a boolean witnessing @p@:
%
\begin{code}
  filter :: (Witness p w) => (x:a -> Bool<w x>)
                          -> List a
                          -> List a<p>
\end{code}

\mypara{To verify} @positives@ we infer the following type and
instantiations for the abstract refinements @p@ and @w@ at the
call to @filter@:
%
\begin{code}
  isPos :: x:Int -> {v:Bool | v <=> 0 < x}
  p     |-> \v    -> 0 < v
  w     |-> \x b  -> b <=> 0 < x
\end{code}

\subsubsection{List Folding}

Next, consider the list fold-right function. Suppose we
wish to prove the following type for @ex3@:
%
\begin{code}
  foldr :: (a -> b -> b) -> b -> List a -> b
  foldr op b []     = b
  foldr op b (x:xs) = x `op` foldr op b xs

  ex3 :: xs:List a -> {v:Int | v == len xs}
  ex3 = foldr (\_ -> incr) 0
\end{code}
%
where @len@ is a \emph{logical} or \emph{measure}
function used to represent the number of elements of
the list in the refinement logic~\ref{subsec:measures}:
%
\begin{code}
  measure len :: List a -> Nat
  len []      = 0
  len (x:xs)  = 1 + len xs
\end{code}

\mypara{We specify induction as a bound.} Let
(1)~@inv@ be an abstract refinement relating a list @xs@
    and the result @b@ obtained by folding over it and
(2)~@step@ be an abstract refinement relating the
    inputs @x@, @b@ and output @b'@ passed to and
    obtained from the accumulator @op@ respectively.
%
We state that @inv@ is closed under @step@ as:
%
\begin{code}
  bound Inductive inv step = \x xs b b' ->
        inv xs b => step x b b' => inv (x:xs) b'
\end{code}
%

\mypara{We can give} @foldr@ a type that says that the
function \emph{outputs} a value that is built inductively
over the entire \emph{input} list:
%
\begin{code}
  foldr :: (Inductive inv step)
        => (x:a -> acc:b -> b<step x acc>)
        -> b<inv []>
        -> xs:List a
        -> b<inv xs>
\end{code}
%
That is, for any invariant @inv@ that is inductive
under @step@, if the initial value @b@ is @inv@-related
to the empty list, then the folded output is @inv@-related
to the input list @xs@.

\mypara{We verify} @ex3@ by inferring, at the call to @foldr@
%
\begin{code}
  inv  |-> \xs v   -> v  == len xs
  step |-> \x b b' -> b' == b + 1
\end{code}
%
The SMT solver validates the VC obtained by plugging the
above into the bound.
%
Instantiating the signature for @foldr@ yields precisely the
output type desired for @ex3@.

%% \nv{addition:}
Previously,~\ref{chapter:abstractrefinements} describes a way to type @foldr@
using abstract refinements that required the operator @op@
to have one extra ghost argument.
Bounds let us express induction without ghost arguments.

\subsection{Implementation}\label{sec:overview:implementation}

To implement bounded refinement typing, we must solve two
problems. Namely, how do we
%
(1)~\emph{check} and
%
(2)~\emph{use}
%
functions with bounded signatures?
%
We solve both problems via an insight inspired
by the way typeclasses are implemented in Haskell.
%
\begin{enumerate}
%
\item \emphbf{A Bound Specifies} a function type
whose inputs are unconstrained and whose output is
some value that carries the refinement corresponding
to the bound's body.
%
\item \emphbf{A Bound is Implemented} by a ghost
function that returns @True@, but is defined
in a context where the bound's constraint holds when
instantiated to the concrete refinements at the context.
%
\end{enumerate}

\mypara{We elaborate bounds into ghost functions} satisfying
the bound's type.
%
To \emph{check} bounded functions, we need to
\emph{call} the ghost function to materialize the
bound constraint at particular values of interest.
%
Dually, to \emph{use} bounded functions, we need to
\emph{create} ghost functions whose outputs are
guaranteed to satisfy the bound constraint.
%
This elaboration reduces \emph{bounded} refinement
typing to the simpler problem
of \emph{unbounded} abstract refinement typing.
%
The formalization of our elaboration is described in
\S~\ref{sec:check}.
%
Next, we illustrate the elaboration by explaining how
it addresses the problems of checking and using bounded
signatures like @compose@.

\mypara{We Translate Bounds into Function Types} called the
bound-type where the inputs are unconstrained, and the
outputs satisfy the bound's constraint.
%
For example, the bound @Chain@ used to type @compose@ in
\S~\ref{sec:compose}, corresponds to a function type, yielding
the translated type for @compose@:
%
\begin{code}
  type ChainTy p q r
    =  x:a -> y:b -> z:c -> {v:Bool | q x y => p y z => r x z}

  compose :: (ChainTy p q r) 
          -> (y:b -> c<p y>)
          -> (x:a -> b<q x>)
          -> (w:a -> c<r w>)
\end{code}

\mypara{To Check Bounded Functions} we view the bound constraints
as extra (ghost) function parameters (cf. type class dictionaries),
that satisfy the bound-type. Crucially, each expression where a
subtyping constraint would be generated (by plain refinement typing)
is wrapped with a ``call'' to the ghost to materialize the constraint
at values of interest. For example we elaborate @compose@ into:
%
\begin{code}
  compose $chain f g x =
    let t1 = g x
        t2 = f t1
        _  = $chain x t1 t2   -- materialize
    in  t2
\end{code}
%
In the elaborated version @$chain@ is the ghost parameter %$
corresponding to the bound. As is standard \cite{LiquidPLDI08},
we perform ANF-conversion to name intermediate values, and then
wrap the function output with a call to the ghost to materialize
the bound's constraint. Consequently, the output of compose, namely
@t2@, is checked to be a subtype of the specified output type,
in an environment \emph{strengthened} with the bound's constraint
instantiated at @x@, @t1@ and @t2@. This subtyping reduces to a
quantifier free VC:
%
\begin{code}
      q x t1
  =>  p t1 t2
  => (q x t1 => p t1 t2 => r x t2)
  =>  v = t2 => r x v
\end{code}
%
whose first two antecedents are due to the types of @t1@ and @t2@
(via the output types of @g@ and @f@ respectively) and the third
comes from the call to @$chain@. %$
%
The output value @v@ has the singleton refinement that
states it equals to @t2@ and finally the VC states that the
output value @v@ must be related to the input @x@ via @r@.
%
An SMT solver validates this decidable VC easily, thereby
verifying @compose@.

Our elaboration inserts materialization calls \emph{for all}
variables (of the appropriate type) that are in scope at the
given point. This could introduce upto $n^k$ calls where $k$
is the number of parameters in the bound and $n$ the number
of variables in scope. In practice (\eg in @compose@) this
number is small (\eg 1) since we limit ourselves to variables
of the appropriate types.

%% Colin's comment: how about strictness and termination?
To preserve semantics we ensure that none of these materialization
calls can diverge, by carefully constraining the structure of
the arguments that instantiate the ghost functional parameters.

\mypara{At Uses of Bounded Functions} our elaboration uses
the bound-type to create lambdas with appropriate parameters
that just return @true@. For example, @ex2@ is elaborated to:
%
\begin{code}
  ex2 = compose (\_ _ _ -> true) incr incr
\end{code}
%
This elaboration seems too na\"ive to be true: how do we
ensure that the function actually satisfies the bound type?

Happily, that is automatically taken care of by function subtyping.
%
Recalling the translated type for @compose@, the elaborated lambda
@(\_ _ _ ->  true)@ is constrained to be a subtype of @ChainTy p q r@.
%
In particular, given the call site instantiation
%
\begin{mcode}
  p $\mapsto$ \ y z -> z == y + 1
  q $\mapsto$ \ x y -> y == x + 1
  r $\mapsto$ \ x z -> z == x + 2
\end{mcode}
%
this subtyping constraint reduces to the quantifier-free VC:
%
\begin{align*}
\inter{\Gamma}
  \Rightarrow \mathtt{true}
  \Rightarrow \cc{(z == y + 1)}
   \Rightarrow \cc{(y == x + 1)}\notag
   \Rightarrow \cc{(z == x + 2)} % \label{vc:bounded:ex2}
\end{align*}
%
%
where $\Gamma$ contains assumptions about the various binders in
scope.
%
The above VC is easily proved valid by an SMT solver, thereby
verifying the subtyping obligation defined by the bound, and hence,
that @ex2@ satisfies the given type.
